#!/usr/bin/awk -f

# Turn a stream of "time=1600297219 state=(ok|err)" into a graph with green/red
# to show availability of service.

function state_glyph(st) {
	if (TTY) {
		color = (st == "err") ? 31 : (st == "ok") ? 32 : 34
		return "\033[1;" color "m|\033[m"
	} else {
		return (st == "err") ? "X" : (st == "ok") ? "|" : ":"
	}
}

function value_flush(ctx)
{
	printf "%s", state_glyph(ctx["state"])
	ctx["state"] = "ok"
	ctx["progress"] += STEP
}

function value_init(ctx)
{
	ctx["progress"] = START
	ctx["status"] = "ok"
}

function value_add(time, value, ctx)
{
	if (time > ctx["progress"])
		value_flush(ctx)

	# catch up with time by filling the gaps with spaces
	while (ctx["progress"] < time) {
		printf " "
		ctx["progress"] += STEP
	}

	# order of priority is "err" > "ok"
	ctx["state"] = (value == "err" ? "err" : ctx["state"])
}

function split_var(input, var,
	key, fields, i)
{
	split(input, fields, " ")
	for (i in fields) {
		key = substr(fields[i], 1, index(fields[i], "=") - 1)
		var[key] = substr(fields[i], index(fields[i], "=") + 1)
	}
}

BEGIN {
	cmd = "date +%s"
	cmd | getline NOW
	close(cmd)

	STEP = ENVIRON["STEP"] ? ENVIRON["STEP"] : 60
	COUNT = ENVIRON["COUNT"] ? ENVIRON["COUNT"] : 80
	NOW = NOW - NOW % STEP + STEP
	START = NOW - STEP * COUNT
	START = START - START % STEP + STEP

	TTY = system("exec tty >/dev/null 2>&1") == 0 && !("NOCOLOR" in ENVIRON)

	value_init(ctx)

	printf "  %s >", START
}

{ split_var($0, var) }

var["time"] >= START {
	value_add(var["time"], var["state"], ctx)
}

{ split_var("", var) }

END {
	value_add(NOW, -1, ctx)
	printf "< %s\n", NOW
}
